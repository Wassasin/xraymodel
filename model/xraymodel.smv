MODULE input
	VAR
		signal : {SelectXPlane, PrepSeqHigh, StartSeqHigh, StopSeqHigh, UnprepSeqHigh, StartOneHigh, StopOneHigh, LowFrontOn, LowFrontOff, LowSideOn, LowSideOff, LowBothOn, LowBothOff, Null};
	ASSIGN
		init(signal) := Null;

MODULE output
	VAR
		mode : {Sequential, Single, Off};
		direction : {Front, Side, Both};
		dose : {Low, High};
		prepared : boolean;
	ASSIGN
		init(mode) := Off;
		init(direction) := Front;
		init(dose) := Low;
		init(prepared) := FALSE;

MODULE errors
	VAR
		FrontHighError : boolean;
		SideHighError : boolean;
		FrontLowError : boolean;
		SideLowError : boolean;
	ASSIGN
		init(FrontHighError) := FALSE;
		init(SideHighError) := FALSE;
		init(FrontLowError) := FALSE;
		init(SideLowError) := FALSE;
	
		next(FrontHighError) := {TRUE, FALSE};
		next(SideHighError) := {TRUE, FALSE};
		next(FrontLowError) := {TRUE, FALSE};
		next(SideLowError) := {TRUE, FALSE};

MODULE main
	VAR
		i : input;
		o : output;
		e : errors;

		instr : {SelectFrontPlane, SelectSidePlane, SelectBothPlane, PrepSeqHigh, StartSeqHigh, StopSeqHigh, UnprepSeqHigh, StartOneHigh, StopOneHigh, LowFrontOn, LowFrontOff, LowSideOn, LowSideOff, LowBothOn, LowBothOff, Null};

		next_direction : {Front, Side, Both};

		low_direction : {Front, Side, Both, DegradedFront, DegradedSide};
		high_single_direction : {Front, Side, Both};
		high_sequential_direction : {Front, Side, Both};
		
		low_mode : boolean;
		high_single_mode : boolean;
		high_sequential_mode : boolean;
		
		high_front_blocked : boolean;
		high_side_blocked : boolean;
		low_front_blocked : boolean;
		low_side_blocked : boolean;
	ASSIGN
		init(instr) := Null;
		init(next_direction) := Front;
		init(low_direction) := Front;
		init(high_single_direction) := Front;
		init(high_sequential_direction) := Front;
		init(low_mode) := FALSE;
		init(high_single_mode) := FALSE;
		init(high_sequential_mode) := FALSE;
		init(high_front_blocked) := FALSE;
		init(high_side_blocked) := FALSE;
		init(low_front_blocked) := FALSE;
		init(low_side_blocked) := FALSE;

		next(i.signal) := case
				i.signal = Null & instr = Null : {SelectXPlane, PrepSeqHigh, StartSeqHigh, StopSeqHigh, UnprepSeqHigh, StartOneHigh, StopOneHigh, LowFrontOn, LowFrontOff, LowSideOn, LowSideOff, LowBothOn, LowBothOff, Null};
				instr = Null & !signal_valid : Null; -- throw away invalid signal
				instr != Null : Null;
				TRUE : i.signal;
		esac;

		next(instr) := case
				done : Null;
				resolved : instr;
				!signal_valid : instr;
				i.signal = Null : instr;
				i.signal = SelectXPlane & next_direction = Front : SelectSidePlane;
				i.signal = SelectXPlane & next_direction = Side : SelectBothPlane;
				i.signal = SelectXPlane & next_direction = Both : SelectFrontPlane;
				i.signal = PrepSeqHigh : PrepSeqHigh;
				i.signal = StartSeqHigh : StartSeqHigh;
				i.signal = StopSeqHigh : StopSeqHigh;
				i.signal = UnprepSeqHigh : UnprepSeqHigh;
				i.signal = StartOneHigh : StartOneHigh;
				i.signal = StopOneHigh : StopOneHigh;
				i.signal = LowFrontOn : LowFrontOn;
				i.signal = LowFrontOff : LowFrontOff;
				i.signal = LowSideOn : LowSideOn;
				i.signal = LowSideOff : LowSideOff;
				i.signal = LowBothOn : LowBothOn;
				i.signal = LowBothOff : LowBothOff;
		esac;

		next(next_direction) := case
				!resolved : next_direction; -- Do nothing while instr is not resolved
				instr = SelectSidePlane : Side;
				instr = SelectBothPlane : Both;
				instr = SelectFrontPlane : Front;
				TRUE : next_direction;
		esac;

		next(low_direction) := case
				!resolved : low_direction;
				instr = LowFrontOn & !low_mode : Front;
				instr = LowSideOn & !low_mode : Side;
				instr = LowBothOn & !low_mode & !low_front_blocked & !low_side_blocked : Both;
				instr = LowBothOn & !low_mode & low_front_blocked : DegradedSide;
				instr = LowBothOn & !low_mode & low_side_blocked : DegradedFront;
				TRUE : low_direction;
		esac;
		
		next(high_single_direction) := case
				!resolved : high_single_direction;
				instr = StartOneHigh : next_direction;
				TRUE : high_single_direction;
		esac;

		next(high_sequential_direction) := case
				!resolved : high_sequential_direction;
				instr = PrepSeqHigh: next_direction;
				TRUE : high_sequential_direction;
		esac;

		next(low_mode) := case
				!resolved : low_mode;
				instr in {LowFrontOn, LowSideOn, LowBothOn} : TRUE;
				instr = LowFrontOff & low_direction = Front : FALSE;
				instr = LowSideOff & low_direction = Side : FALSE;
				instr = LowBothOff & low_direction in {Both, DegradedFront, DegradedSide} : FALSE;
				TRUE : low_mode;
		esac;

		next(high_single_mode) := case
				!resolved : high_single_mode;
				instr = StartOneHigh : TRUE;
				instr = StopOneHigh & high_single_mode : FALSE;
				TRUE : high_single_mode;
		esac;
		
		next(high_sequential_mode) := case
				!resolved : high_sequential_mode;
				instr = StartSeqHigh : TRUE;
				instr = StopSeqHigh & high_sequential_mode : FALSE;
				TRUE : high_sequential_mode;
		esac;

		next(o.mode) := case
				high_sequential_mode & o.mode = Sequential & high_sequential_direction = Front & high_front_blocked : Off;
				high_sequential_mode & o.mode = Sequential & high_sequential_direction = Side & high_side_blocked : Off;
				high_sequential_mode & o.mode = Sequential & high_sequential_direction = Both & (high_front_blocked | high_side_blocked) : Off;
				
				high_single_mode & o.mode = Single & high_single_direction = Front & high_front_blocked : Off;
				high_single_mode & o.mode = Single & high_single_direction = Side & high_side_blocked : Off;
				high_single_mode & o.mode = Single & high_single_direction = Both & (high_front_blocked | high_side_blocked) : Off;
				
				high_sequential_mode & high_single_mode : o.mode; -- If both are set, one was set earlier, and thus has precendence
				high_sequential_mode : Sequential;
				high_single_mode : Single;
				
				low_mode & low_direction in {Front, DegradedFront} & low_front_blocked : Off;
				low_mode & low_direction in {Side, DegradedSide} & low_side_blocked : Off;
				low_mode & low_direction = Both & (low_front_blocked | low_side_blocked) : Off;

				low_mode : Sequential;
				
				!low_mode : Off;
		esac;

		next(o.direction) := case		
				high_sequential_mode & high_single_mode : o.direction; -- If both are set, one was set earlier, and thus has precendence
				high_sequential_mode : high_sequential_direction;
				high_single_mode : high_single_direction;
				
				low_mode & low_direction = DegradedFront : Front;
				low_mode & low_direction = DegradedSide : Side;

				low_mode : low_direction;
				
				TRUE : o.direction;
		esac;

		next(o.dose) := case
				high_sequential_mode | high_single_mode : High;
				low_mode : Low;
				TRUE : o.dose;
		esac;

		next(o.prepared) := case
				error_high : FALSE;
				!resolved : o.prepared;
				instr = PrepSeqHigh : TRUE;
				instr = UnprepSeqHigh : FALSE;
				TRUE : o.prepared;
		esac;
		
		next(high_front_blocked) := case
				e.FrontHighError : TRUE;
				dose_high & error_high : TRUE; -- requirement 14, when high_dose and an error, block all
				!high_single_mode & !high_sequential_mode : FALSE;
				TRUE : high_front_blocked;
		esac;

		next(high_side_blocked) := case
				e.SideHighError : TRUE;
				dose_high & error_high : TRUE; -- requirement 14, when high_dose and an error, block all
				!high_single_mode & !high_sequential_mode : FALSE;
				TRUE : high_side_blocked;
		esac;
		
		next(low_front_blocked) := case
				e.FrontLowError : TRUE;
				!low_mode : FALSE;
				TRUE : low_front_blocked;
		esac;
		
		next(low_side_blocked) := case
				e.SideLowError : TRUE;
				!low_mode : FALSE;
				TRUE : low_side_blocked;
		esac;

	DEFINE
		resolved := (
				i.signal = Null &
				instr != Null
		);

		done := (
				done_errors &
				(instr != Null) &
				(instr = SelectSidePlane -> next_direction = Side) &
				(instr = SelectBothPlane -> next_direction = Both) &
				(instr = SelectFrontPlane -> next_direction = Front) &
				(instr = PrepSeqHigh -> o.prepared) &
				(instr = StartSeqHigh -> (dose_highseq | (dose_highone & high_sequential_mode))) &
				(instr = StopSeqHigh -> !dose_highseq) &
				(instr = UnprepSeqHigh -> !o.prepared) &
				(instr = StartOneHigh -> (dose_highone | (dose_highseq & high_single_mode))) &
				(instr = StopOneHigh -> !dose_highone) &
				(instr = LowFrontOn & !low_front_blocked ->
						low_mode &
						low_direction = Front & (
								(high_sequential_mode | high_single_mode) | (
										dose_low &
										o.direction = Front
								)
						)
				) &
				(instr = LowFrontOff -> !low_mode & ((high_sequential_mode | high_single_mode) | o.mode = Off)) &
				(instr = LowSideOn & !low_side_blocked ->
						low_mode &
						low_direction = Side & (
								(high_sequential_mode | high_single_mode) | (
										dose_low &
										o.direction = Side
								)
						)
				) &
				(instr = LowSideOff -> !low_mode & ((high_sequential_mode | high_single_mode) | o.mode = Off)) &
				(instr = LowBothOn & !low_front_blocked & !low_side_blocked ->
						low_mode &
						low_direction = Both & (
								(high_sequential_mode | high_single_mode) | (
										dose_low &
										o.direction = Both
								)
						)
				) &
				(instr = LowBothOn & low_front_blocked & !low_side_blocked ->
						low_mode &
						low_direction = DegradedSide & (
								(high_sequential_mode | high_single_mode) | (
										dose_low &
										o.direction = Side
								)
						)
				) &
				(instr = LowBothOn & !low_front_blocked & low_side_blocked ->
						low_mode &
						low_direction = DegradedFront & (
								(high_sequential_mode | high_single_mode) | (
										dose_low &
										o.direction = Front
								)
						)
				) &
				(instr = LowBothOn & low_front_blocked & low_side_blocked -> low_mode & !dose_low) &
				(instr = LowBothOff -> !low_mode & ((high_sequential_mode | high_single_mode) | o.mode = Off))
		);
		
		done_errors := (
			(e.FrontLowError -> low_front_blocked) &
			(e.SideLowError -> low_side_blocked) &
			(e.FrontHighError -> high_front_blocked) &
			(e.SideHighError -> high_side_blocked) &
			(dose_high & error_high -> high_front_blocked & high_side_blocked) & -- requirement 14, when high_dose and an error, block all
			
			(!e.FrontLowError & !low_mode -> !low_front_blocked) &
			(!e.SideLowError & !low_mode -> !low_side_blocked) &
			(!e.FrontHighError & !high_single_mode & !high_sequential_mode -> !high_front_blocked) &
			(!e.SideHighError & !high_single_mode & !high_sequential_mode -> !high_side_blocked) &
			
			(low_front_blocked -> !(low_direction in {Front, Both, DegradedFront} & low_mode) & !(o.direction in {Front, Both} & dose_low)) &
			(low_side_blocked -> !(low_direction in {Side, Both, DegradedSide} & low_mode) & !(o.direction in {Side, Both} & dose_low)) &
			(high_front_blocked -> !(high_sequential_direction in {Front, Both} & high_single_direction in {Front, Both} & (high_sequential_mode | high_single_mode)) & !(o.direction in {Front, Both} & dose_high)) &
			(high_side_blocked -> !(high_sequential_direction in {Side, Both} & high_single_direction in {Side, Both} & (high_sequential_mode | high_single_mode)) & !(o.direction in {Side, Both} & dose_high))
		);

		signal_valid := (
				(i.signal = SelectXPlane -> TRUE) & -- Possible to do with any error, errors handled at output-stage, see mail 26-06-2013 14:43
				(i.signal = PrepSeqHigh -> !high_sequential_mode & !o.prepared) &
				(i.signal = StartSeqHigh -> !high_sequential_mode & o.prepared & !blocked_high_current) &
				(i.signal = StopSeqHigh -> high_sequential_mode) &
				(i.signal = UnprepSeqHigh -> !high_sequential_mode & o.prepared) &
				(i.signal = StartOneHigh -> !high_single_mode & !blocked_high_current) &
				(i.signal = StopOneHigh -> high_single_mode) &
				(i.signal = LowFrontOn -> !low_mode & !low_front_blocked) &
				(i.signal = LowFrontOff -> low_mode & low_direction = Front) &
				(i.signal = LowSideOn -> !low_mode & !low_side_blocked) &
				(i.signal = LowSideOff -> low_mode & low_direction = Side) &
				(i.signal = LowBothOn -> !low_mode) &
				(i.signal = LowBothOff -> low_mode & low_direction in {Both, DegradedFront, DegradedSide})
		);
		
		blocked_high_current := (
				(next_direction = Front & high_front_blocked) |
				(next_direction = Side & high_side_blocked) |
				(next_direction = Both & (high_front_blocked | high_side_blocked))
		);

		dose_no := o.mode = Off;
		dose_low := o.dose = Low & o.mode = Sequential;
		dose_high := o.dose = High & o.mode in {Sequential, Single};
		dose_highseq := o.dose = High & o.mode = Sequential;
		dose_highone := o.dose = High & o.mode = Single;
		low_signal := i.signal in {LowFrontOn, LowSideOn, LowBothOn};
		high_signal := i.signal in {StartOneHigh, StartSeqHigh};
		error_any := error_low | error_high;
		error_high := e.FrontHighError | e.SideHighError;
		error_highboth := e.FrontHighError & e.SideHighError;
		error_low := e.FrontLowError | e.SideLowError;
		error_lowboth := e.FrontLowError & e.SideLowError;


	-- General requirement 1
	LTLSPEC (G !error_any) -> G ((dose_low & Y dose_no) -> Y(dose_no S low_signal))
	LTLSPEC (G !error_any) -> G ((dose_highseq & Y dose_no) -> Y(dose_no S i.signal = StartSeqHigh))
	LTLSPEC (G !error_any) -> G ((dose_highone & Y dose_no) -> Y(dose_no S i.signal = StartOneHigh))

	-- General requirement 2
	LTLSPEC G (e.FrontLowError & dose_low -> F !(dose_low & o.direction = Front))
	LTLSPEC G (e.SideLowError & dose_low -> F !(dose_low & o.direction = Side))
	LTLSPEC G (e.FrontHighError & dose_high -> F !(dose_high & o.direction = Front))
	LTLSPEC G (e.SideHighError & dose_high -> F !(dose_high & o.direction = Side))

	-- General requirement 3
	LTLSPEC G (dose_no & i.signal = StartSeqHigh & next_direction = Front & (!e.FrontHighError U dose_highseq) -> F dose_highseq)
	LTLSPEC G (dose_no & i.signal = StartSeqHigh & next_direction = Side & (!e.SideHighError U dose_highseq) -> F dose_highseq)
	LTLSPEC G (dose_no & i.signal = StartSeqHigh & next_direction = Both & (!error_highboth U dose_highseq) -> F dose_highseq)
	LTLSPEC G (dose_no & i.signal = StartOneHigh & next_direction = Front & (!e.FrontHighError U dose_highone) -> F dose_highone)
	LTLSPEC G (dose_no & i.signal = StartOneHigh & next_direction = Side & (!e.SideHighError U dose_highone) -> F dose_highone)
	LTLSPEC G (dose_no & i.signal = StartOneHigh & next_direction = Both & (!error_highboth U dose_highone) -> F dose_highone)
	LTLSPEC G (dose_no & i.signal = LowFrontOn & ((!e.FrontLowError & !high_signal) U dose_low) -> F dose_low)
	LTLSPEC G (dose_no & i.signal = LowSideOn & ((!e.SideLowError & !high_signal) U dose_low) -> F dose_low)
	LTLSPEC G (dose_no & i.signal = LowBothOn & ((!error_lowboth & !high_signal) U dose_low) -> F dose_low)

	-- General requirement 4
	LTLSPEC (G !error_any) -> ! F (
		( dose_high & !low_mode & i.signal = LowFrontOn ) &
		( dose_high & !low_mode & i.signal = LowFrontOn ) U (
			( dose_high & !low_signal ) &
			( dose_high & !low_signal ) U (
				( dose_high & i.signal = LowFrontOff ) &
				( dose_high & i.signal = LowFrontOff ) U (
					( dose_high & !low_signal ) &
					( dose_high & !low_signal ) U (
						( !dose_high & !low_signal) &		-- low dose or no dose
						( !dose_high & !low_signal) U (
							dose_low
	))))))
	
	LTLSPEC (G !error_any) -> ! F (
		( dose_high & !low_mode & i.signal = LowSideOn ) &
		( dose_high & !low_mode & i.signal = LowSideOn ) U (
			( dose_high & !low_signal ) &
			( dose_high & !low_signal ) U (
				( dose_high & i.signal = LowSideOff ) &
				( dose_high & i.signal = LowSideOff ) U (
					( dose_high & !low_signal ) &
					( dose_high & !low_signal ) U (
						( !dose_high & !low_signal) &		-- low dose or no dose
						( !dose_high & !low_signal) U (
							dose_low
	))))))
	
	LTLSPEC (G !error_any) -> ! F (
		( dose_high & !low_mode & i.signal = LowBothOn ) &
		( dose_high & !low_mode & i.signal = LowBothOn ) U (
			( dose_high & !low_signal ) &
			( dose_high & !low_signal ) U (
				( dose_high & i.signal = LowBothOff ) &
				( dose_high & i.signal = LowBothOff ) U (
					( dose_high & !low_signal ) &
					( dose_high & !low_signal ) U (
						( !dose_high & !low_signal) &		-- low dose or no dose
						( !dose_high & !low_signal) U (
							dose_low
	))))))
	
	LTLSPEC (G !error_any) -> ! F (
		( dose_low & i.signal = StartSeqHigh ) &
		( dose_low & i.signal = StartSeqHigh ) U (
			( dose_low & !high_signal ) &
			( dose_low & !high_signal ) U (
				( dose_low & i.signal = StopSeqHigh ) &
				( dose_low & i.signal = StopSeqHigh ) U (
					( dose_low & !high_signal ) &
					( dose_low & !high_signal ) U (
						( !dose_low & !high_signal) &		-- high dose or no dose
						( !dose_low & !high_signal) U (
							dose_high
	))))))
	
	LTLSPEC (G !error_any) -> ! F (
		( dose_low & i.signal = StartOneHigh ) &
		( dose_low & i.signal = StartOneHigh ) U (
			( dose_low & !high_signal ) &
			( dose_low & !high_signal ) U (
				( dose_low & i.signal = StopOneHigh ) &
				( dose_low & i.signal = StopOneHigh ) U (
					( dose_low & !high_signal ) &
					( dose_low & !high_signal ) U (
						( !dose_low & !high_signal) &		-- high dose or no dose
						( !dose_low & !high_signal) U (
							dose_high
	))))))

	-- Dose requirement 5
	LTLSPEC (G !error_any) -> G (dose_highseq & (F instr = StopSeqHigh) -> (dose_highseq U instr = StopSeqHigh)) -- aanname: gewijzigd xplane is geen nieuwe request
	LTLSPEC (G !error_any) -> G (dose_highone & (F instr = StopOneHigh) -> (dose_highone U instr = StopOneHigh))

	-- Dose requirement 6
	LTLSPEC (G !error_any) -> G (instr = LowFrontOn & !dose_high -> F (dose_low & o.direction = Front))
	LTLSPEC (G !error_any) -> G (instr = LowSideOn & !dose_high -> F (dose_low & o.direction = Side))
	LTLSPEC (G !error_any) -> G (instr = LowBothOn & !dose_high -> F (dose_low & o.direction = Both))
	LTLSPEC (G !error_any) -> G (instr = LowFrontOn & (F i.signal = LowFrontOff) -> (!(instr in {LowSideOn, LowBothOn}) U i.signal = LowFrontOff))
	LTLSPEC (G !error_any) -> G (instr = LowSideOn & (F i.signal = LowSideOff) -> (!(instr in {LowFrontOn, LowBothOn}) U i.signal = LowSideOff))
	LTLSPEC (G !error_any) -> G (instr = LowBothOn & (F i.signal = LowBothOff) -> (!(instr in {LowFrontOn, LowSideOn}) U i.signal = LowBothOff))

	-- Dose requirement 7
	LTLSPEC (G !error_any) -> G (o.direction = Front & dose_low & (F instr in {StartSeqHigh, StartOneHigh}) & (!F instr = LowFrontOff) -> (F dose_high))
	LTLSPEC (G !error_any) -> G (o.direction = Side & dose_low & (F instr in {StartSeqHigh, StartOneHigh}) & (!F instr = LowSideOff) -> (F dose_high))
	LTLSPEC (G !error_any) -> G (o.direction = Both & dose_low & (F instr in {StartSeqHigh, StartOneHigh}) & (!F instr = LowBothOff) -> (F dose_high))

	-- Dose requirement 8
	LTLSPEC G (dose_low & instr = StartSeqHigh & (!error_high U instr = StopSeqHigh) -> (dose_low U dose_high U dose_low)) --toch kijken naar instr = LowBothOn?
	LTLSPEC G (dose_low & instr = StartOneHigh & (!error_high U instr = StopOneHigh) -> (dose_low U dose_high U dose_low))
	
	-- Dose requirement 9
	LTLSPEC (G !error_any) -> G (dose_highseq & low_signal & (F i.signal = StopSeqHigh) -> (o.dose != Low U i.signal = StopSeqHigh))
	LTLSPEC (G !error_any) -> G (dose_highone & low_signal & (F i.signal = StopOneHigh) -> (o.dose != Low U i.signal = StopOneHigh))

	-- Error requirement 10
	LTLSPEC G (!o.prepared & error_high & (F !error_high) -> (!o.prepared U !error_high))
	LTLSPEC G (dose_no & i.signal = LowFrontOn & e.FrontLowError & (F !e.FrontLowError) -> (dose_no U !e.FrontLowError))
	LTLSPEC G (dose_no & i.signal = LowSideOn & e.SideLowError & (F !e.SideLowError) -> (!(dose_low & o.direction = Side) U !e.SideLowError))
	LTLSPEC G (dose_no & i.signal = LowBothOn & error_low & (F !error_low) -> (!(dose_low & o.direction = Both) U !error_low))
	LTLSPEC G (i.signal = StartSeqHigh & error_high & (F !error_high) -> (!dose_highseq U !error_high))
	LTLSPEC G (i.signal = StartOneHigh & error_high & (F !error_high) -> (!dose_highone U !error_high))

	-- Error requirement 11
	LTLSPEC (G !error_high) -> G (dose_no & instr = LowBothOn & e.FrontLowError & (!F e.FrontLowError) -> F(o.direction = Side))
	LTLSPEC (G !error_high) -> G (dose_no & instr = LowBothOn & e.SideLowError & (!F e.SideLowError) -> F(o.direction = Front))

	-- Error requirement 12
	LTLSPEC G (dose_high & error_high -> F(!dose_high))

	-- Error requirement 13
	LTLSPEC G (e.FrontLowError & i.signal = LowFrontOn -> F !(o.direction = Front & dose_low))
	LTLSPEC G (e.SideLowError & i.signal = LowSideOn -> F !(o.direction = Side & dose_low))
	LTLSPEC G (error_lowboth & i.signal = LowBothOn -> F !dose_low)
	
	LTLSPEC G (e.FrontHighError & next_direction = Front & i.signal in {StartSeqHigh, StartOneHigh} -> F !(o.direction = Front & dose_high))
	LTLSPEC G (e.SideHighError & next_direction = Side & i.signal in {StartSeqHigh, StartOneHigh} -> F !(o.direction = Side & dose_high))
	LTLSPEC G (error_high & next_direction = Both & i.signal in {StartSeqHigh, StartOneHigh} -> F !dose_high)

	-- Error requirement 14
	-- For High actually the same as requirement 14
	LTLSPEC G (dose_low & o.direction in {Front, DegradedFront} & e.FrontLowError -> F !dose_low)
	LTLSPEC G (dose_low & o.direction in {Side, DegradedSide} & e.SideLowError -> F !dose_low)
	LTLSPEC G (dose_low & o.direction = Both & (e.FrontLowError | e.SideLowError) -> F !dose_low)
	
	-- Error requirement 15
	-- Due to abstraction not relevant, see report
